// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/RAM8.hdl

/**
 * Memory of 8 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM8 {
    IN in[16], load, address[3];
    OUT out[16];

    PARTS:
        DMux8Way(in=load, sel=address, a=loadA, b=loadB, c=loadC, d=loadD,
            e=loadE, f=loadF, g=loadG, h=loadH);
        Register(in=in, load=loadA, out=valueA);
        Register(in=in, load=loadB, out=valueB);
        Register(in=in, load=loadC, out=valueC);
        Register(in=in, load=loadD, out=valueD);
        Register(in=in, load=loadE, out=valueE);
        Register(in=in, load=loadF, out=valueF);
        Register(in=in, load=loadG, out=valueG);
        Register(in=in, load=loadH, out=valueH);
        Mux8Way16(a=valueA, b=valueB, c=valueC, d=valueD, e=valueE, f=valueF,
            g=valueG, h=valueH, sel=address, out=out);
}
